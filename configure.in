AC_INIT(configure.in)
VERSION=`grep ^Version: $srcdir/vte.spec | awk '{print $NF}'`
AM_INIT_AUTOMAKE(vte,$VERSION)
AM_PROG_LIBTOOL
ALL_LINGUAS="da sv"
AM_GLIB_GNU_GETTEXT
AC_EGREP_CPP(glibc,
		[
		 #include <stdio.h>
		 #ifdef __GLIBC__
		 glibc
		 #endif
		],
		AC_DEFINE(_GNU_SOURCE,1,[Use all features.]))
# AC_SYS_LARGEFILE
AC_PATH_XTRA
PKG_CHECK_MODULES(GLIB,[glib-2.0 gobject-2.0])
PKG_CHECK_MODULES(GTK, [glib-2.0 gobject-2.0 gdk-pixbuf-2.0 gtk+-2.0 pangox])
if pkg-config --exists pangoxft '>=' 1.1.0 ; then
	havexft=1
	AC_DEFINE(HAVE_XFT2,1,[Whether we have Xft version 2])
	PKG_CHECK_MODULES(XFT,[xft])
else
	savelibs="$LIBS"
	LIBS="$GTK_LIBS"
	AC_CHECK_FUNC(XftDrawString32,
		      [AC_CHECK_HEADER(X11/Xft/Xft.h, havexft=1)])
	LIBS="$savelibs"
fi
AC_DEFINE(G_DISABLE_DEPRECATED,1,[Disable deprecated glib features.])
AC_DEFINE(GDK_DISABLE_DEPRECATED,1,[Disable deprecated gdk features.])
AC_DEFINE(GDK_PIXBUF_DISABLE_DEPRECATED,1,[Disable deprecated gdk-pixbuf features.])
AC_DEFINE(GTK_DISABLE_DEPRECATED,1,[Disable deprecated gtk features.])
AC_DEFINE(PANGO_DISABLE_DEPRECATED,1,[Disable deprecated pango features.])
AC_DEFINE(VTE_UTF8_BPC,6,[Maximum number of bytes used per UTF-8 character.])
AC_DEFINE_UNQUOTED(PACKAGE,"$PACKAGE",[Package name.])
AC_CHECK_FUNCS(getpt grantpt unlockpt ptsname ptsname_r)

# Double-check that the GTK libraries pulled in Xft functionality, then use
# the GTK CFLAGS to find the Xft headers.
savelibs="$LIBS"
LIBS="$GTK_LIBS"
havexft=0
AC_CHECK_FUNC(XftDrawString32,havexft=1)
LIBS="$savelibs"
if test x$havexft = x1 ; then
	AC_DEFINE(HAVE_XFT, 1, [Whether not Xft is available.])
fi

AM_MAINTAINER_MODE

if test x$USE_MAINTAINER_MODE != x ; then
	if test x$USE_MAINTAINER_MODE != xno ; then
		if test x$GCC = xyes ; then
			CFLAGS="${CFLAGS} -std=c99"
			CFLAGS="${CFLAGS} -Wunused"
			CFLAGS="${CFLAGS} -Wuninitialized"
			CFLAGS="${CFLAGS} -Wimplicit"
			CFLAGS="${CFLAGS} -Wstrict-prototypes"
			CFLAGS="${CFLAGS} -Wmissing-prototypes"
			CFLAGS="${CFLAGS} -Wmissing-declarations"
			CFLAGS="${CFLAGS} -Wcast-align"
		fi
		VTE_DEBUG=1
	fi
fi
if test x$VTE_DEBUG = x1 ; then
	AC_DEFINE(VTE_DEBUG,,[Enable debugging messages.])
fi
AC_CHECK_TYPE(wint_t,AC_DEFINE(HAVE_WINT_T,1,[Define if your OS provides a wint_t type and a corresponding %lc format specifier.]))

AC_MSG_CHECKING([for pygtk-2.0])
if pkg-config --exists pygtk-2.0 ; then
	AC_MSG_RESULT([found])
	PKG_CHECK_MODULES(PYGTK,[pygtk-2.0])
	PYGTK_DATADIR=`pkg-config --variable=datadir pygtk-2.0`
	AC_SUBST(PYGTK_DATADIR)
	AM_PATH_PYTHON
	AC_MSG_CHECKING(for python >= 2.2)
	prog="
import sys, string
minver = (2,2,0,'final',0)
if sys.version_info < minver:
  sys.exit(1)
sys.exit(0)"
	if $PYTHON -c "$prog" 1>&AC_FD_CC 2>&AC_FD_CC
	then
	  AC_MSG_RESULT(okay)
          AM_CHECK_PYTHON_HEADERS([PYTHONMODULES="vtemodule.so"],
			          [AC_MSG_RESULT(could not find Python headers)])
	else
	  AC_MSG_RESULT(too old)
	fi
else
	AC_MSG_RESULT([not found])
fi
AC_SUBST(PYTHONREV)
AC_SUBST(PYTHONMODULES)

mydatadir=`eval echo $datadir`
if test x$prefix = xNONE ; then
	mydatadir=`echo $mydatadir | sed s,NONE,$ac_default_prefix,g`
fi
AC_DEFINE_UNQUOTED(DATADIR,"$mydatadir",
                   [The location where arch-independent package-specific data can be found.])
AC_DEFINE_UNQUOTED(LOCALEDIR,"$mydatadir/locale",
                   [The location where locale data can be found.])

GETTEXT_PACKAGE=vte
AC_SUBST(GETTEXT_PACKAGE)

AM_CONFIG_HEADER(config.h)
AC_OUTPUT([
Makefile
src/Makefile
termcaps/Makefile
python/Makefile
po/Makefile.in
vte.pc
])
